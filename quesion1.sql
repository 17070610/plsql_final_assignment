CREATE TABLE access_violations (
  violation_id    NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  username        VARCHAR2(128),
  action_type     VARCHAR2(50),
  object_name     VARCHAR2(128),
  attempted_at    TIMESTAMP DEFAULT SYSTIMESTAMP,
  client_info     VARCHAR2(4000),
  details         CLOB
);

CREATE TABLE employees (
    id NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
    name VARCHAR2(50),
    position VARCHAR2(50),
    salary NUMBER
);


CREATE OR REPLACE PROCEDURE log_access_violation (
  p_username    IN VARCHAR2,
  p_action_type IN VARCHAR2,
  p_object_name IN VARCHAR2,
  p_details     IN CLOB DEFAULT NULL
) AS
  PRAGMA AUTONOMOUS_TRANSACTION;
BEGIN
  INSERT INTO access_violations (
    username, action_type, object_name, attempted_at, client_info, details
  ) VALUES (
    NVL(p_username, 'UNKNOWN'),
    p_action_type,
    p_object_name,
    SYSTIMESTAMP,
    SYS_CONTEXT('USERENV','IP_ADDRESS') || ' / SID=' || SYS_CONTEXT('USERENV','SID') || ' / OSUSER=' || SYS_CONTEXT('USERENV','OS_USER'),
    p_details
  );
  COMMIT;
EXCEPTION WHEN OTHERS THEN NULL;
END;
/

CREATE OR REPLACE FUNCTION is_allowed_access_time RETURN BOOLEAN IS
  v_day_name   VARCHAR2(20);
  v_time_hh24  NUMBER;
BEGIN
  v_day_name := TRIM(UPPER(TO_CHAR(SYSDATE, 'DAY', 'NLS_DATE_LANGUAGE=ENGLISH')));
  v_time_hh24 := TO_NUMBER(TO_CHAR(SYSDATE, 'HH24') || TO_CHAR(SYSDATE, 'MI'));

  IF v_day_name NOT IN ('MONDAY','TUESDAY','WEDNESDAY','THURSDAY','FRIDAY') THEN
    RETURN FALSE;
  END IF;

  IF v_time_hh24 < 800 OR v_time_hh24 > 1700 THEN
    RETURN FALSE;
  END IF;

  RETURN TRUE;
EXCEPTION WHEN OTHERS THEN
  RETURN FALSE;
END;
/

CREATE OR REPLACE TRIGGER trg_auca_emp_dml_restrict
  BEFORE INSERT OR UPDATE OR DELETE ON employees
  FOR EACH ROW
DECLARE
  v_user VARCHAR2(128);
  v_action VARCHAR2(20);
BEGIN
  v_user := NVL(USER, SYS_CONTEXT('USERENV','SESSION_USER'));

  IF NOT is_allowed_access_time THEN
    IF INSERTING THEN
      v_action := 'INSERT';
    ELSIF UPDATING THEN
      v_action := 'UPDATE';
    ELSIF DELETING THEN
      v_action := 'DELETE';
    ELSE
      v_action := 'DML';
    END IF;

    log_access_violation(
      p_username    => v_user,
      p_action_type => 'DML_' || v_action,
      p_object_name => 'EMPLOYEES',
      p_details     => 'Blocked ' || v_action || ' by user ' || v_user || ' outside allowed hours.'
    );

    RAISE_APPLICATION_ERROR(-20002, 'DML not allowed outside permitted hours (Mon-Fri 08:00-17:00). Action blocked and logged.');
  END IF;
END trg_auca_emp_dml_restrict;
/
