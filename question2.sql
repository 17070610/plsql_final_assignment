CREATE TABLE employees (
  emp_id    NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
  emp_no    VARCHAR2(20) UNIQUE,
  first_name VARCHAR2(50),
  last_name  VARCHAR2(50),
  salary     NUMBER(12,2),
  hired_date DATE DEFAULT SYSDATE
);

COMMIT;

INSERT INTO employees (emp_no, first_name, last_name, salary) VALUES ('E001','John','Doe', 5000);
INSERT INTO employees (emp_no, first_name, last_name, salary) VALUES ('E002','Mary','Smith', 4500);
INSERT INTO employees (emp_no, first_name, last_name, salary) VALUES ('E003','Alice','Mugabe', 6000);

CREATE TABLE salary_audit (
  audit_id NUMBER GENERATED BY DEFAULT AS IDENTITY PRIMARY KEY,
  emp_id   NUMBER,
  old_salary NUMBER(12,2),
  new_salary NUMBER(12,2),
  changed_by VARCHAR2(128),
  changed_at TIMESTAMP DEFAULT SYSTIMESTAMP,
  note_text CLOB
);

COMMIT;

CREATE OR REPLACE PACKAGE hr_payroll_pkg IS

  FUNCTION calc_rssb(p_salary IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER;

  FUNCTION net_salary_from_gross(p_salary IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER;

  FUNCTION get_emp_net_salary(p_emp_id IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER;

  PROCEDURE dynamic_salary_operation(p_action IN VARCHAR2, p_emp_id IN NUMBER, p_amount IN NUMBER DEFAULT NULL);

  PROCEDURE process_bulk_rssb(p_rate_pct IN NUMBER DEFAULT 3.5);

END hr_payroll_pkg;
/

CREATE OR REPLACE PACKAGE BODY hr_payroll_pkg IS

  FUNCTION calc_rssb(p_salary IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER IS
    v_rssb NUMBER(12,2);
  BEGIN
    IF p_salary IS NULL THEN
      RETURN NULL;
    END IF;
    v_rssb := ROUND(p_salary * (p_rate_pct/100), 2);
    RETURN v_rssb;
  END calc_rssb;

  FUNCTION net_salary_from_gross(p_salary IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER IS
    v_rssb NUMBER(12,2);
  BEGIN
    v_rssb := calc_rssb(p_salary, p_rate_pct);
    RETURN ROUND(NVL(p_salary,0) - NVL(v_rssb,0), 2);
  END net_salary_from_gross;

  FUNCTION get_emp_net_salary(p_emp_id IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER IS
    v_salary NUMBER(12,2);
    v_net    NUMBER(12,2);
  BEGIN
    SELECT salary INTO v_salary FROM employees WHERE emp_id = p_emp_id;
    v_net := net_salary_from_gross(v_salary, p_rate_pct);
    RETURN v_net;
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RETURN NULL;
    WHEN OTHERS THEN
      RAISE;
  END get_emp_net_salary;

  PROCEDURE dynamic_salary_operation(p_action IN VARCHAR2, p_emp_id IN NUMBER, p_amount IN NUMBER DEFAULT NULL) IS
    v_sql  VARCHAR2(4000);
    v_old  NUMBER(12,2);
    v_new  NUMBER(12,2);
  BEGIN
    IF p_action = 'UPDATE_SALARY' THEN
      IF p_amount IS NULL THEN
        RAISE_APPLICATION_ERROR(-20010, 'UPDATE_SALARY requires p_amount (new salary).');
      END IF;

      SELECT salary INTO v_old FROM employees WHERE emp_id = p_emp_id;
      v_new := p_amount;

      v_sql := 'UPDATE employees SET salary = :1 WHERE emp_id = :2';
      EXECUTE IMMEDIATE v_sql USING v_new, p_emp_id;

      INSERT INTO salary_audit(emp_id, old_salary, new_salary, changed_by, note_text)
      VALUES(p_emp_id, v_old, v_new, USER, 'Updated salary via dynamic_salary_operation');
      COMMIT;

    ELSIF p_action = 'INCREMENT_SALARY' THEN
      IF p_amount IS NULL THEN
        RAISE_APPLICATION_ERROR(-20011, 'INCREMENT_SALARY requires p_amount (increment).');
      END IF;

      SELECT salary INTO v_old FROM employees WHERE emp_id = p_emp_id;
      v_new := v_old + p_amount;
      v_sql := 'UPDATE employees SET salary = salary + :1 WHERE emp_id = :2';
      EXECUTE IMMEDIATE v_sql USING p_amount, p_emp_id;

      INSERT INTO salary_audit(emp_id, old_salary, new_salary, changed_by, note_text)
      VALUES(p_emp_id, v_old, v_new, USER, 'Incremented salary via dynamic_salary_operation');
      COMMIT;

    ELSIF p_action = 'INSERT_AUDIT' THEN
      v_new := p_amount;
      v_sql := 'INSERT INTO salary_audit(emp_id, old_salary, new_salary, changed_by, note_text) VALUES (:1, NULL, :2, :3, :4)';
      EXECUTE IMMEDIATE v_sql USING p_emp_id, v_new, USER, 'Dynamic audit insert';
      COMMIT;

    ELSE
      RAISE_APPLICATION_ERROR(-20012, 'Unknown action: ' || p_action);
    END IF;
  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RAISE_APPLICATION_ERROR(-20020, 'Employee not found: ' || p_emp_id);
    WHEN OTHERS THEN
      RAISE;
  END dynamic_salary_operation;

  PROCEDURE process_bulk_rssb(p_rate_pct IN NUMBER DEFAULT 3.5) IS
    CURSOR c_emp IS SELECT emp_id, salary FROM employees;
    v_net NUMBER(12,2);
  BEGIN
    FOR r IN c_emp LOOP
      v_net := net_salary_from_gross(r.salary, p_rate_pct);
      INSERT INTO salary_audit(emp_id, old_salary, new_salary, changed_by, note_text)
      VALUES (r.emp_id, r.salary, v_net, USER, 'Bulk RSSB report: net salary recorded');
    END LOOP;
    COMMIT;
  END process_bulk_rssb;

END hr_payroll_pkg;
/

CREATE OR REPLACE PACKAGE hr_payroll_invoker_pkg AUTHID CURRENT_USER IS
  FUNCTION get_emp_net_salary_invoker(p_emp_id IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER;
END hr_payroll_invoker_pkg;
/

CREATE OR REPLACE PACKAGE BODY hr_payroll_invoker_pkg IS
  FUNCTION get_emp_net_salary_invoker(p_emp_id IN NUMBER, p_rate_pct IN NUMBER DEFAULT 3.5) RETURN NUMBER IS
  BEGIN
    RETURN hr_payroll_pkg.get_emp_net_salary(p_emp_id, p_rate_pct);
  EXCEPTION WHEN OTHERS THEN
    RAISE;
  END;
END hr_payroll_invoker_pkg;
/

SELECT hr_payroll_pkg.calc_rssb(5000) AS rssb_amount FROM dual;

SELECT hr_payroll_pkg.net_salary_from_gross(5000) AS net_salary FROM dual;

SELECT hr_payroll_pkg.get_emp_net_salary(1) AS emp1_net FROM dual;

BEGIN
  hr_payroll_pkg.dynamic_salary_operation('UPDATE_SALARY', 1, 5200);
END;
/

SELECT emp_id, salary, hr_payroll_pkg.net_salary_from_gross(salary) AS net FROM employees WHERE emp_id = 1;

BEGIN
  hr_payroll_pkg.dynamic_salary_operation('INCREMENT_SALARY', 2, 200);
END;
/

BEGIN
  hr_payroll_pkg.process_bulk_rssb(3.5);
END;
/

SELECT * FROM salary_audit ORDER BY audit_id DESC FETCH FIRST 20 ROWS ONLY;

